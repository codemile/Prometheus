"Name"     = 'Prometheus Grammar'
"Author"   = 'ThinkingMedia'
"Version"  = '1.0'
"About"    = 'Scripting language for processing documents.'

"Start Symbol" = <Program>

! ===================================================================
! Character Sets
! ===================================================================

{ID Head}      = {Letter} + [_]
{ID Tail}      = {Alphanumeric} + [_]
{DoubleString Chars} = {Printable} + {HT} - ["\]
{SingleString Chars} = {Printable} + {HT} - [''\]

! ===================================================================
! Terminals
! ===================================================================

Identifier		= {ID Head}{ID Tail}*
StringDouble	= '"' ( {DoubleString Chars} | '\' {Printable} )* '"'
StringSingle	= [''] ( {SingleString Chars} | '\' {Printable} )* ['']
Integer			= [0123456789,]+
Float			= [0123456789,]+ '.' [0123456789]+
Boolean			= true | false | on | off | yes | no | always | never

! ===================================================================
! Comments
! ===================================================================

Comment Start = '/*'
Comment End   = '*/'
Comment Line  = '//'

! ===================================================================
! Math Expressions
! ===================================================================
<Expression>  ::= <Expression> '>'  <AddExpression> 
			   |  <Expression> '<'  <AddExpression> 
			   |  <Expression> '<=' <AddExpression> 
			   |  <Expression> '>=' <AddExpression>
			   |  <Expression> '==' <AddExpression>    !Equal
			   |  <Expression> '<>' <AddExpression>    !Not equal
			   |  <Expression> '!=' <AddExpression>    !Not equal
			   |  <Expression> '&&' <AddExpression>
			   |  <Expression> 'and' <AddExpression>
			   |  <Expression> '||' <AddExpression>
			   |  <Expression> 'or' <AddExpression>
			   |  <AddExpression> 

<AddExpression>			::= <AddExpression> '+' <SubExpression>
						 |  <SubExpression> 

<SubExpression>			::= <SubExpression> '-' <MultiplyExpression>
						 |  <MultiplyExpression> 

<MultiplyExpression>    ::= <MultiplyExpression> '*' <DivideExpression> 
						 |  <DivideExpression> 

<DivideExpression>		::= <DivideExpression> '/' <SearchTerm> 
						 |  <SearchTerm> 

<SearchTerm>			::= 'has' <UnaryOperator>
						 | 'contains' <UnaryOperator>
						 | <UnaryOperator>

<UnaryOperator>			::= '-' <Value>
						 | '+' <Value>
						 | '!' <Value>
						 | '~' <Value>
						 | 'not' <Value>
						 | <Value>

! ===================================================================
! Arguments for commands
! ===================================================================

<Variable>	  ::= Identifier

<Value>       ::= <Variable>
			   | StringDouble
			   | StringSingle
			   | Integer
			   | Float
			   | Boolean
			   | '(' <Expression> ')'
			  
! ===================================================================
! Basic structure of the program.
! ===================================================================
<Program> ::= <Statements>
		   
<Statements> ::= <Statement>
				| <Statements>  <Statement>

<IfControl> ::= IF '(' <Expression> ')' <Statements> END IF
<IfElseControl> ::= IF '(' <Expression> ')' <Statements> ELSE <Statements> END IF
<WhileControl> ::= WHILE '(' <Expression> ')' <Statements> END WHILE
<DoControl> ::= DO <Statements> WHILE '(' <Expression> ')'

! ===================================================================
! Statements
! ===================================================================
<Statement> ::= <SetCommand>
			 | <UnsetCommand>
			 | <RejectCommand>
			 | <AcceptCommand>
			 | <ScopeCommand>
			 | <IncludeCommand>
			 | <PrintCommand>
			 | <UpperCommand>
			 | <LowerCommand>
			 | <TrimCommand>
			 | <Expression>

! ===================================================================
! Variable Commands
! ===================================================================

<SetCommand>		::= SET <Variable> '=' <Expression>
<UnsetCommand>		::= UNSET <Variable>

! ===================================================================
! Directives
! ===================================================================

<IncludeCommand>	::= INCLUDE <Expression>

! ===================================================================
! Output Commands
! ===================================================================

<PrintCommand>		::= PRINT '(' <Statement> ')'

! ===================================================================
! String Manipulation
! ===================================================================

<UpperCommand>		::= UPPER '(' <Statement> ')'
<LowerCommand>		::= LOWER '(' <Statement> ')'
<TrimCommand>		::= TRIM '(' <Statement> ')'

! ===================================================================
! Context Commands
! ===================================================================

<ScopeCommand>		::= SCOPE '(' <Statement> ')'
<RejectCommand>		::= REJECT '(' <Statement> ')'
<AcceptCommand>		::= ACCEPT '(' <Statement> ')'
