"Name"     = 'Prometheus Grammar'
"Author"   = 'ThinkingMedia'
"Version"  = '1.0'
"About"    = 'Scripting language for processing documents.'
"Case Sensitive" = 'False'
"Start Symbol" = <Program>

! ===================================================================
! Change NewLine to a terminal
! ===================================================================
{WS} = {Whitespace} - {CR} - {LF}
Whitespace = {WS}+
NewLine = {CR}{LF}|{CR}

! ===================================================================
! Character Sets
! ===================================================================

{ID Head}				= {Letter} + [_]
{ID Tail}				= {Alphanumeric} + [_]
{DoubleString Chars}	= {Printable} + {HT} - ["\]
{SingleString Chars}	= {Printable} + {HT} - [''\]

! ===================================================================
! Terminals
! ===================================================================

Identifier		= {ID Head}{ID Tail}*
StringDouble	= '"' ( {DoubleString Chars} | '\' {Printable} )* '"'
StringSingle	= [''] ( {SingleString Chars} | '\' {Printable} )* ['']
Number			= [0123456789,]+
Decimal			= [0123456789,]+ '.' [0123456789]+
Boolean			= true | false | on | off | yes | no | always | never

! ===================================================================
! Comments
! ===================================================================

Comment Start = '/*'
Comment End   = '*/'
Comment Line  = '//'

! ===================================================================
! Math Expressions
! ===================================================================
<Expression>  ::= <GtOperator>

<GtOperator>			::= <GtOperator> '>' <LtOperator> | <LtOperator>
<LtOperator>			::= <LtOperator> '<' <GteOperator> | <GteOperator>
<GteOperator>			::= <GteOperator> '>=' <LteOperator> | <LteOperator>
<LteOperator>			::= <LteOperator> '<=' <EqualOperator> | <EqualOperator>

<EqualOperator>			::= <EqualOperator> '==' <NotEqualOperator> | <NotEqualOperator>
<NotEqualOperator>		::= <NotEqualOperator> '!=' <AndOperator> | <NotEqualOperator> '<>' <AndOperator> | <AndOperator>
<AndOperator>			::= <AndOperator> AND <OrOperator> | <AndOperator> '&&' <OrOperator> | <OrOperator>
<OrOperator>			::= <OrOperator> OR <AddExpression> | <OrOperator> '||' <AddExpression> | <AddExpression>

<AddExpression>			::= <AddExpression> '+' <SubExpression> | <SubExpression> 
<SubExpression>			::= <SubExpression> '-' <MultiplyExpression> | <MultiplyExpression> 
<MultiplyExpression>    ::= <MultiplyExpression> '*' <DivideExpression>  | <DivideExpression> 
<DivideExpression>		::= <DivideExpression> '/' <UnaryOperator>  | <UnaryOperator> 

<UnaryOperator>			::= '--' <Variable>
						 | '++' <Variable>
						 | '-' <Value>
						 | '+' <Value>
						 | '!' <Value>
						 | '~' <Value>
						 | NOT <Value>
						 | <Value>

! ===================================================================
! Variables And Primitives
! ===================================================================

<Variable>		::= Identifier

<Value>			::= StringDouble
				| StringSingle
				| Number
				| Decimal
				| Boolean
				| <Variable>
				| <Function>
				| '(' <Expression> ')'

! ===================================================================
! Statements
! ===================================================================
<Program>		::= <Statements>

<Statements>	::= <Statement>
				| <Statements> <Statement>

<Statement>		::= <FlowControl> NewLine
				| <Variables> NewLine
				| <Procedure> NewLine
				| <Function> NewLine
				| NewLine

! ===================================================================
! Flow control
! ===================================================================

<FlowControl>		::= <IfControl>
					| <DoWhileControl>
					| <DoUntilControl>
					| <LoopWhileControl>
					| <LoopUntilControl>
					| <BreakControl>
					| <ContinueControl>

<IfControl>			::= IF <Expression> THEN NewLine <Statements> <EndIfControl>
<EndIfControl>		::= END IF 
					| ELSE NewLine <Statements> END IF
					| ELSEIF <Expression> THEN NewLine <Statements> <EndIfControl>

<DoWhileControl>	::= DO WHILE <Expression> NewLine <Statements> LOOP
					| WHILE <Expression> NewLine <Statements> END WHILE
<DoUntilControl>	::= DO UNTIL <Expression> NewLine <Statements> LOOP
					| UNTIL <Expression> NewLine <Statements> END UNTIL
<LoopWhileControl>	::= DO NewLine <Statements> LOOP WHILE <Expression>
<LoopUntilControl>	::= DO NewLine <Statements> LOOP UNTIL <Expression>

<BreakControl>		::= BREAK
<ContinueControl>	::= CONTINUE

! ===================================================================
! Variable Commands
! ===================================================================

<Variables>			::= <Declare>
					| <ListVars>
					| <Increment>
					| <Decrement>

<Declare>			::= VAR Identifier
					| VAR Identifier '=' <Expression>

<ListVars>			::= VARS

<Increment>			::= Identifier '++'
<Decrement>			::= Identifier '--'

! ===================================================================
! Procedures
! ===================================================================

<Procedure>	::= <UnsetProc>
			 | <RejectProc>
			 | <AcceptProc>
			 | <ScopeProc>
			 | <IncludeProc>
			 | <PrintProc>

<UnsetProc>			::= UNSET Identifier
<IncludeProc>		::= INCLUDE <Expression>
<PrintProc>			::= PRINT <Expression>
<ScopeProc>			::= SCOPE <Expression>
<RejectProc>		::= REJECT <Expression>
<AcceptProc>		::= ACCEPT <Expression>

! ===================================================================
! Functions
! ===================================================================

<Function>	::= <UpperFunc>
			 | <LowerFunc>
			 | <TrimFunc>

<UpperFunc>			::= UPPER '(' <Expression> ')'
<LowerFunc>			::= LOWER '(' <Expression> ')'
<TrimFunc>			::= TRIM '(' <Expression> ')'

