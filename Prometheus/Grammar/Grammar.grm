"Name"     = 'Prometheus Grammar'
"Author"   = 'ThinkingMedia'
"Version"  = '1.0'
"About"    = 'Scripting language for processing documents.'
"Case Sensitive" = 'False'
"Start Symbol" = <Program>

! ===================================================================
! Change NewLine to a terminal
! ===================================================================
{WS} = {Whitespace} - {CR} - {LF}
Whitespace = {WS}+
NewLine = {CR}{LF}|{CR}

! ===================================================================
! Character Sets
! ===================================================================

{ID Head}				= {Letter} + [_]
{ID Tail}				= {Alphanumeric} + [_]
{DoubleString Chars}	= {Printable} + {HT} - ["\]
{SingleString Chars}	= {Printable} + {HT} - [''\]

! ===================================================================
! Terminals
! ===================================================================

Identifier		= {ID Head}{ID Tail}*
StringDouble	= '"' ( {DoubleString Chars} | '\' {Printable} )* '"'
StringSingle	= [''] ( {SingleString Chars} | '\' {Printable} )* ['']
Number			= [0123456789,]+
Decimal			= [0123456789,]+ '.' [0123456789]+
Boolean			= true | false | on | off | yes | no | always | never

! ===================================================================
! Comments
! ===================================================================

Comment Start = '/*'
Comment End   = '*/'
Comment Line  = '//'

! ===================================================================
! Math Expressions
! ===================================================================
<Expression>  ::= <Expression> '>'  <AddExpression> 
			   |  <Expression> '<'  <AddExpression> 
			   |  <Expression> '<=' <AddExpression> 
			   |  <Expression> '>=' <AddExpression>
			   |  <Expression> '==' <AddExpression>    !Equal
			   |  <Expression> '<>' <AddExpression>    !Not equal
			   |  <Expression> '!=' <AddExpression>    !Not equal
			   |  <Expression> '&&' <AddExpression>
			   |  <Expression> '||' <AddExpression>
			   |  <Expression> AND <AddExpression>
			   |  <Expression> OR <AddExpression>
			   |  <AddExpression> 

<AddExpression>			::= <AddExpression> '+' <SubExpression>
						 |  <SubExpression> 

<SubExpression>			::= <SubExpression> '-' <MultiplyExpression>
						 |  <MultiplyExpression> 

<MultiplyExpression>    ::= <MultiplyExpression> '*' <DivideExpression> 
						 |  <DivideExpression> 

<DivideExpression>		::= <DivideExpression> '/' <UnaryOperator> 
						 |  <UnaryOperator> 

<UnaryOperator>			::= '--' <Variable>
						 | '++' <Variable>
						 | '-' <Value>
						 | '+' <Value>
						 | '!' <Value>
						 | '~' <Value>
						 | NOT <Value>
						 | <Value>

! ===================================================================
! Variables And Primitives
! ===================================================================

<Variable>		::= Identifier

<Value>			::= StringDouble
				| StringSingle
				| Number
				| Decimal
				| <Variable>
				| <Function>
				| '(' <Expression> ')'

! ===================================================================
! Statements
! ===================================================================
<Program> ::= <Statements>

<Statements> ::= <Statement>
				| <Statements>  <Statement>

<Statement> ::= <Assignment> NewLine
			 | <Procedure> NewLine
			 | <Function> NewLine
			 | <Increment> NewLine
			 | <Decrement> NewLine
			 | NewLine

<Function>	::= <UpperFunc>
			 | <LowerFunc>
			 | <TrimFunc>

<Procedure>	::= <UnsetProc>
			 | <RejectProc>
			 | <AcceptProc>
			 | <ScopeProc>
			 | <IncludeProc>
			 | <PrintProc>

! ===================================================================
! Flow control
! ===================================================================

<IfControl> ::= IF <Expression> NewLine <Statements> NewLine END NewLine
<IfElseControl> ::= IF <Expression> NewLine <Statements> NewLine ELSE NewLine <Statements> END NewLine
<WhileControl> ::= WHILE <Expression> NewLine <Statements> NewLine END NewLine
<DoControl> ::= DO NewLine <Statements> NewLine WHILE <Expression> NewLine

! ===================================================================
! Variable Commands
! ===================================================================

<Assignment>		::= VAR Identifier '=' <Expression>
<Increment>			::= Identifier '++'
<Decrement>			::= Identifier '--'

! ===================================================================
! Procedures
! ===================================================================

<UnsetProc>			::= UNSET Identifier
<IncludeProc>		::= INCLUDE <Expression>
<PrintProc>			::= PRINT <Expression>
<ScopeProc>			::= SCOPE <Expression>
<RejectProc>		::= REJECT <Expression>
<AcceptProc>		::= ACCEPT <Expression>

! ===================================================================
! Functions
! ===================================================================

<UpperFunc>			::= UPPER '(' <Expression> ')'
<LowerFunc>			::= LOWER '(' <Expression> ')'
<TrimFunc>			::= TRIM '(' <Expression> ')'

